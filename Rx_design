module uart_rx #
(
    parameter CLKS_PER_BIT = 434   // 50MHz / 115200 baud
)
(
    input  wire       clk,
    input  wire       rst,
    input  wire       rx,
    output reg [7:0]  rx_data,
    output reg        rx_done
);

    // FSM States
    localparam IDLE  = 3'd0;
    localparam START = 3'd1;
    localparam DATA  = 3'd2;
    localparam STOP  = 3'd3;
    localparam DONE  = 3'd4;

    reg [2:0]  state;
    reg [15:0] clk_cnt;
    reg [2:0]  bit_idx;
    reg [7:0]  shift_reg;

    always @(posedge clk or posedge rst) begin
        if (rst) begin
            state     <= IDLE;
            clk_cnt  <= 0;
            bit_idx  <= 0;
            rx_done  <= 0;
        end else begin
            case (state)

            // IDLE 

            IDLE: begin
                rx_done <= 0;
                clk_cnt <= 0;
                bit_idx <= 0;

                if (rx == 1'b0)   // Detect start bit
                    state <= START;
            end

            // START BIT CHECK

            START: begin
                if (clk_cnt == (CLKS_PER_BIT/2)) begin
                    if (rx == 1'b0) begin
                        clk_cnt <= 0;
                        state <= DATA;
                    end else
                        state <= IDLE;   // False start bit
                end else
                    clk_cnt <= clk_cnt + 1;
            end

            //DATA BITS 

            DATA: begin
                if (clk_cnt < CLKS_PER_BIT-1)
                    clk_cnt <= clk_cnt + 1;
                else begin
                    clk_cnt <= 0;
                    shift_reg[bit_idx] <= rx;

                    if (bit_idx < 7)
                        bit_idx <= bit_idx + 1;
                    else begin
                        bit_idx <= 0;
                        state <= STOP;
                    end
                end
            end

            // STOP BIT

            STOP: begin
                if (clk_cnt < CLKS_PER_BIT-1)
                    clk_cnt <= clk_cnt + 1;
                else begin
                    rx_data <= shift_reg;
                    rx_done <= 1;
                    clk_cnt <= 0;
                    state <= DONE;
                end
            end

            //  DONE 

            DONE: begin
                rx_done <= 0;
                state <= IDLE;
            end

            default: state <= IDLE;

            endcase
        end
    end

endmodule
